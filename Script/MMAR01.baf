IF
	OnCreation()
	!Global("ExitMMAR01Unsealed", "GLOBAL", 1)
	Global("EnteredDerilGuestRoom", "GLOBAL", 0)
THEN
	RESPONSE #100
		SetGlobal("EnteredDerilGuestRoom", "GLOBAL", 1)
		TriggerActivation("TranMM1004", FALSE)
		
		CreateCreature("MMSASTZI", [629.436], 7)
		CreateCreature("MMVERISS", [464.340], 14)
		CreateCreature("MMLAGOLE", [610.281], 0)
		CreateCreature("MMDERIL", [668.303], 6)
		CreateCreature("MMLAROTT", [557.491], 7)
		CreateCreature("MMMARBLE", [400.364], 14)
		Continue()
END

// Only visual
IF
	Global("SealExitMMAR01", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("SealExitMMAR01", "GLOBAL", 2)
		PlaySound("EFF_E01")
		CreateVisualEffect("SPCMWOUI", [223.551])
END

IF
	Global("PillarEscapesPeacefully", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("PillarEscapesPeacefully", "GLOBAL", 2)
		AddExperienceParty(50000)
		
		StartCutSceneMode()
		ActionOverride("celich", ReallyForceSpell(Myself, DRYAD_TELEPORT))
		ActionOverride("verissa", ReallyForceSpell(Myself, DRYAD_TELEPORT))
		ActionOverride("sastzi", ReallyForceSpell(Myself, DRYAD_TELEPORT))
		ActionOverride("larottle", ReallyForceSpell(Myself, DRYAD_TELEPORT))
		ActionOverride("marbled", ReallyForceSpell(Myself, DRYAD_TELEPORT))
		Wait(2)
		EndCutSceneMode()
		
		SetQuestDone(@15)
		SetQuestDone(@16)
		AddJournalEntry(@51,QUEST_DONE)
		SetGlobalTimer("CerndReactDerilQuestDone01", "GLOBAL", TWO_ROUNDS)
		SetGlobal("DerilQuestDone", "GLOBAL", 1)
END

IF
	Global("PillarMembersSlain", "GLOBAL", 5)
	Global("DerilMansionUpdateJournal", "GLOBAL", 0)
THEN
	RESPONSE #100
		SetGlobal("DerilMansionUpdateJournal", "GLOBAL", 1)
		SetQuestDone(@15)
		SetQuestDone(@16)
		AddJournalEntry(@50,QUEST_DONE)
		SetGlobalTimer("CerndReactDerilQuestDone02", "GLOBAL", TWO_ROUNDS)
		SetGlobal("DerilQuestDone", "GLOBAL", 1)
END

// Opening up exit to the house in no-karsomir-stolen case
IF
	Global("ExitMMAR01Unsealed", "GLOBAL", 0)
	Global("StealKarsomir", "GLOBAL", 0)
	OR(2)
		Global("PillarEscapesPeacefully", "GLOBAL", 2)
		Global("PillarMembersSlain", "GLOBAL", 5)
THEN
	RESPONSE #100
		SetGlobal("ExitMMAR01Unsealed", "GLOBAL", 1)
		PlaySound("EFF_E01")
		CreateVisualEffect("SPCMWOUI", [223.551])
		DisplayString(Player1, @60)
		TriggerActivation("TranMM1004", "TRUE")
END


// Opening up exit to the house in karsomir-stolen case
IF
	Global("ExitMMAR01Unsealed", "GLOBAL", 0)
	GlobalGT("StealKarsomir", "GLOBAL", 0)
	Global("PillarMembersSlain", "GLOBAL", 4)
THEN
	RESPONSE #100
		SetGlobal("ExitMMAR01Unsealed", "GLOBAL", 1)
		PlaySound("EFF_E01")
		CreateVisualEffect("SPCMWOUI", [223.551])
		DisplayString(Player1, @60)
		TriggerActivation("TranMM1004", "TRUE")
END
	
		